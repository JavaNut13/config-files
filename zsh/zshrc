#!/bin/zsh

source ~/.zsh/alias.sh
source ~/.zsh/env.sh

# Prompt
autoload -U colors
colors
setopt prompt_subst
source ~/.zsh/hostname_color.sh
PROMPT='%(?.%F{$hostname_color}^_^%f.%F{red}x_x%f) (%1/) '

# Visual suggestions
source ~/.zsh/zsh-autosuggestions/zsh-autosuggestions.zsh

# gcd command

source ~/.zsh/gcd.sh


_tmux_pane_words() {
  local expl
  local -a w
  if [[ -z "$TMUX_PANE" ]]; then
    _message "not running inside tmux!"
    return 1
  fi
  w=( ${(u)=$(tmux capture-pane -J -p)} )
  _wanted values expl 'words from current tmux pane' compadd -a w
}
zle -C tmux-pane-words-prefix   complete-word _generic
zle -C tmux-pane-words-anywhere complete-word _generic
bindkey '^Xt' tmux-pane-words-prefix
bindkey '^X^X' tmux-pane-words-anywhere
zstyle ':completion:tmux-pane-words-(prefix|anywhere):*' completer _tmux_pane_words
zstyle ':completion:tmux-pane-words-(prefix|anywhere):*' ignore-line current
zstyle ':completion:tmux-pane-words-anywhere:*' matcher-list 'b:=* m:{A-Za-z}={a-zA-Z}'

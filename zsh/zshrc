#!/bin/zsh

funcpath="$HOME/.shell/autoload"
FPATH="$funcpath:$FPATH"
autoload ${=$(cd "$funcpath" && echo *)}
unset funcpath

source ~/.shell/alias.sh
source ~/.zsh/completions.zsh

# ALIASES
for mode in +x -x +r -r +w -r 777 755; do
  eval "$mode() { chmod $mode \"\$@\" }"
done
alias -g CL=" | wc -l"
alias -g CC=" | wc -c"
alias -g LESS=" | less"

alias -g '::'=" | less"
alias -g '??'=" | grep -E"
alias -g ':?'=" | grep -vE"

local short='...'
local long='../..'
for dots in {1..10}; do
  alias -g $short=$long
  short="${short}."
  long="$long/.."
done

# Prompt
setopt prompt_subst
PROMPT='%(?.%F{$HOST_COLOR}.%F{red})||>%f '

# History
setopt EXTENDED_HISTORY
setopt HIST_IGNORE_DUPS
setopt HISTIGNORESPACE
HISTSIZE=1000000
SAVEHIST=1000000
HISTFILE=~/._zsh_history
setopt inc_append_history share_history

REPORTTIME=10

# Vim key bindings

bindkey -v
export KEYTIMEOUT=50

bindkey -M viins 'jk' vi-cmd-mode
bindkey -M viins 'Jk' vi-cmd-mode
bindkey -M viins 'JK' vi-cmd-mode
bindkey -M viins 'jK' vi-cmd-mode
# Allow deleting into non-inserted text
bindkey "^?" backward-delete-char
# I do "jkk" to run the prev command, and if I miss the j I want it to still work
alias kk=r

function zle-line-init zle-keymap-select {
  VIM_PROMPT="-- NORMAL --"
  RPS1="${${KEYMAP/vicmd/$VIM_PROMPT}/(main|viins)/}"
  zle reset-prompt
}

preexec() {
  local d=`date +"%H:%M:%S"`
  local c=$(($COLUMNS-9))
  echo -e "\033[1A\033[${c}C ${d}"
}

zle -N zle-line-init
zle -N zle-keymap-select

export HIGHLIGHTING_ENABLED=true
# Load config specific to this device
__local_file="$HOME/.zshrc-local"
if [ -e $__local_file ]; then
  source $__local_file
fi

# Pug installed
source "$HOME/.pug/source/zsh/pug"

export FZF_TMUX_OPTS="-p"
export FZF_CTRL_R_OPTS="--reverse --preview 'echo {}' --preview-window down:3:hidden:wrap --bind '?:toggle-preview'"

if [ -d /usr/share/doc/fzf/examples ]; then
  source /usr/share/doc/fzf/examples/key-bindings.zsh
fi

if [ -z "$TMUX" ] && [ -n "$SSH_CLIENT" ]; then
  case $- in
    *i*) print_welcome; tmux attach || mx tmux ;;
  esac
fi
